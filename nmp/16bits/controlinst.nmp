op controlinst16 = call16|jumps16|ret_16|loop_16|fret16

op jumps16 = j16|jeq16|jgez|jgtz|jlez|jne16|jltz|jz16|jnz_16|ji_16

op call16(const:int(8))
   syntax = format("call %08x", __IADDR+coerce(int(32),const)*2)
   image = format("%8b01011100",const)
   action = 	{ 
	  	if (FCX == 0)
		then
			error("TRAP call16:FCX==0");//TRAP(FCU)
		endif;
		cdc_incr(TMP_1);
		if (CDEFLAG && TMP_1)
		then
			error("TRAP call16:CDEFLAG==1");//TRAP(CDO)
		endif;
		CDEFLAG = 1;
		TMP_1 = PC+2;
		TMP_2 = FCX;
		TMP_3 = FCXS<0..3>::0b000000::FCXO<0..15>::0b000000;
		loadWord(TMP_4,TMP_3);
		storeWord(PCXI,TMP_3);
		storeWord(PSW,TMP_3+4);
		storeWord(A[10],TMP_3+8);
		storeWord(A[11],TMP_3+12);
		storeWord(D[8],TMP_3+16);
		storeWord(D[9],TMP_3+20);
		storeWord(D[10],TMP_3+24);
		storeWord(D[11],TMP_3+28);
		storeWord(A[12],TMP_3+32);
		storeWord(A[13],TMP_3+36);
		storeWord(A[14],TMP_3+40);
		storeWord(A[15],TMP_3+44);
		storeWord(D[12],TMP_3+48);
		storeWord(D[13],TMP_3+52);
		storeWord(D[14],TMP_3+56);
		storeWord(D[15],TMP_3+60);
		PCPN = CCPN;
		PIE = IE;
		UL = 1;
		PCXI<0..19> = FCX<0..19>;
		FCX<0..19> = TMP_4<0..19>;
		NPC = PC + coerce(int(32),2*const);
		A[11] = TMP_1;
		if (TMP_2 == LCX)
		then 
			error("TRAP call16:LCX==FCX");//TRAP(FCD)
		endif;
   		}
   		otawa_sem = "SAVE_CONTEXT; SETI(A11, IADDR + 2); SETI(T1, IADDR + 2 * S8(0)); BRANCH(T1);"

op j16 (const:int(8))	
   syntax = format("j %08x", __IADDR+coerce(int(32),const)*2)
   image = format("%8b00111100", const)
   action = 	{ 
	  	NPC = PC + coerce(int(32),const)*2;
   		}
   	otawa_sem = "SETI(T1, IADDR + S8(0) * 2); BRANCH(T1);"

op jeq16 = jeq16_const_1|jeq16_const_2|jeq16_reg_1|jeq16_reg_2

op jeq16_const_1(const:const4, disp:card(4))
	syntax = format("jeq d15, %s, %08x", const.syntax, __IADDR+coerce(card(32),const)*2)
	image = format("%s %4b %8b", const.image, disp, 0x1E)
	action = {
		sign_ext4to32(const, TMP_1);
		zero_ext32(disp, TMP_2, 4);
		TMP_2=2*TMP_2;
		if (D[15]==TMP_1) then
			NPC=PC+TMP_2;
		endif;
	}
	
op jeq16_const_2(const:const4, disp:card(4))
	syntax = format("jeq d15, %s, %08x", const.syntax, __IADDR+coerce(card(32),disp+16)*2)
	image = format("%s %4b %8b", const.image, disp, 0x9E)
	action = {
		sign_ext4to32(const, TMP_1);
		zero_ext32(disp, TMP_2, 4); // disp+16 will make 16 get inverted to 0
		TMP_2=TMP_2+16;
		TMP_2=2*TMP_2;
		if (D[15]==TMP_1) then
			NPC=PC+TMP_2;
		endif;
	}
	
op jeq16_reg_1(b:reg_d, disp:card(4))
	syntax = format("jeq d15, %s, %08x", b.syntax, __IADDR+coerce(card(32),disp)*2)
	image = format("%s %4b %8b", b.image, disp, 0x3E)
	action = {
		zero_ext32(disp, TMP_2, 4);
		TMP_2=2*TMP_2;
		if (D[15]==b) then
			NPC=PC+TMP_2;
		endif;
	}
	
op jeq16_reg_2(b:reg_d, disp:card(4))
	syntax = format("jeq d15, %s, %08x", b.syntax, __IADDR+coerce(card(32),disp+16)*2)
	image = format("%s %4b %8b", b.image, disp, 0xBE)
	action = {
		TMP_2 = disp;
		TMP_2 = TMP_2 + 16;
		TMP_2 = TMP_2*2;
		if (D[15]==b) then
			NPC=PC+TMP_2;
		endif;
	}

op jgez (b:reg_d, disp:card(4))
    syntax = format("jgez %s, %08x", b.syntax, __IADDR+coerce(card(32),disp)*2)
    image = format("%s%4b11001110",b.image,disp)
    action = 	{
    	TMP_6_i32 = b;
		if(TMP_6_i32 >= 0)
		then 
			NPC = PC + coerce(card(32),disp)*2; 
		endif;
		}
	otawa_sem = "SETI(T1, 0), CMP(T1, D(0), T1); IF(GE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jgtz (b:reg_d,disp:card(4))
    syntax = format("jgtz %s, %08x", b.syntax, __IADDR+coerce(card(32),disp)*2)
    image = format("%s%4b01001110",b.image,disp)
    action = 	{
    	TMP_6_i32 = b;
		if(TMP_6_i32 >= 0)
		then 
			NPC = PC + coerce(card(32),disp)*2; 
		endif;
		}
	otawa_sem = "SETI(T1, 0), CMP(T1, D(0), T1); IF(GT, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jlez (b:reg_d,disp:card(4))
    syntax = format("jlez %s, %08x", b.syntax, __IADDR+coerce(card(32),disp)*2)
    image = format("%s%4b10001110",b.image,disp)
    action = 	{
    	TMP_6_i32 = b;
		if(TMP_6_i32 <= 0)
		then 
			NPC = PC + coerce(card(32),disp)*2; 
		endif;
		}
	otawa_sem = "SETI(T1, 0), CMP(T1, D(0), T1); IF(LE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jltz (b:reg_d, disp:card(4))
    syntax = format("jltz %s, %08x", b.syntax, __IADDR+coerce(card(32),disp)*2)
    image = format("%s%4b00001110",b.image,disp)
    action = 	{
    	TMP_6_i32 = b;
		if(TMP_6_i32 < 0) // or check the 31st bit is 1
		then
			NPC = PC + coerce(card(32),disp)*2;
		endif;
		}
	otawa_sem = "SETI(T1, 0), CMP(T1, D(0), T1); IF(LE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jne16 = jne16_reg_1|jne16_const_1|jne16_reg_2|jne16_const_2

op jne16_const_1 (const4:int(4), disp:card(4))
    syntax = format("jne d15, %d, %08x", const4,__IADDR+coerce(card(32),disp)*2)
    image = format("%4b%4b01011110",const4,disp)
    action = 	{
		if(D[15]!=coerce(int(32),const4))
		then 
			NPC = PC + coerce(card(32),disp)*2; 
		endif;
		}
	otawa_sem = "SETI(T1, S4(0)), CMP(T1, D15, T1); IF(NE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jne16_const_2 (const4:int(4), disp:card(4))
    syntax = format("jne d15, %d, %08x", const4, __IADDR+coerce(card(32),disp+16)*2)
    image = format("%4b%4b 1101 1110 ",const4,disp)
    action = 	{
		if(D[15]!=coerce(int(32),const4))
		then 
			NPC = PC + coerce(card(32),disp+16)*2; 
		endif;
		}

op jne16_reg_1 (b:reg_d, disp:card(4))
   syntax = format("jne d15, %s, %08x", b.syntax, __IADDR+coerce(card(32),disp)*2)
   image = format("%s%4b01111110",b.image,disp)
   action = 	{
		if(D[15]!=b)
		then 
			NPC = PC + coerce(card(32),disp)*2; 
		endif;
		}
	otawa_sem = "CMP(T1, D15, D(0)); IF(NE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jne16_reg_2 (b:reg_d, disp:card(4))
   syntax = format("jne d15, %s, %08x", b.syntax, __IADDR+coerce(card(32),disp+16)*2)
   image = format("%s%4b 1111 1110 ",b.image,disp)
   action = 	{
		if(D[15]!=b)
		then 
			NPC = PC + coerce(card(32),disp+16)*2; 
		endif;
		}

op jz16 = jz_t_16|jz_disp4_16|jz_disp8_16|jz_a_16

op jz_t_16 (n:card(4), disp:card(4))
   syntax = format("jz.t d15, %d, %08x", n, __IADDR+coerce(card(32),disp)*2)
   image = format("%4b%4b00101110",n,disp)
   action = 	{ 
		if (D[15]<n..n> == 0)
		then 
			NPC = PC + coerce(card(32),disp)*2; 
		endif; 
		}
	otawa_sem = "SCRATCH(T1); IF(NE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jz_disp8_16(disp:int(8))
   syntax = format ("jz d15, %08x", __IADDR+coerce(int(32),disp)*2)	
   image = format("%8b01101110",disp)
   action = 	{
	  	if (D[15]==0) 
		then
	     		NPC = PC + coerce(int(32),disp)*2;
	  	endif;
   		}
	otawa_sem = "SETI(T1, 0); CMP(T1, D15, T1); IF(EQ, T1, 2); SETI(T1, IADDR + 2 * S8(0)); BRANCH(T1);"

op jz_disp4_16 (b:reg_d, disp:card(4))
   syntax = format ("jz %s, %08x", b.syntax,__IADDR+coerce(card(32),disp)*2)
   image = format("%s%4b01110110",b.image,disp)
   action = 	{
	  	if (b==0) 
		then
	     		NPC = PC + coerce(card(32),disp)*2;
	  	endif;
   }
	otawa_sem = "SETI(T1, 0); CMP(T1, D(0), T1); IF(EQ, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1); "

op ret_16 (foo:card(4))
   syntax = "ret"
   image = format("1001%4b00000000",foo)
   action = 	{
		cdc_decr(TMP_1);
		if (CDEFLAG && TMP_1)
		then
			error("TRAP ret16:CDEFLAG==CDC");//TRAP(CDU)
		endif;
		if(PCXI<0..19> == 0)
		then
			error("TRAP ret16:PCXI<0..19>==0");//TRAP(CSU)
		endif;
		if (UL == 0)
		then
			error("TRAP ret16:UL==0");//TRAP(CTYP)
		endif;
		NPC = A[11]<31..1>::0b0;
		TMP_3 = PCXS<0..3>::0b000000::PCXO<0..15>::0b000000;
		loadWord(TMP_1,TMP_3); 		// new_PCXI
		loadWord(TMP_2,TMP_3+4); 	// new_PSW
		loadWord(A[10],TMP_3+8);
		loadWord(A[11],TMP_3+12);
		loadWord(D[8],TMP_3+16);
		loadWord(D[9],TMP_3+20);
		loadWord(D[10],TMP_3+24);
		loadWord(D[11],TMP_3+28);
		loadWord(A[12],TMP_3+32);
		loadWord(A[13],TMP_3+36);
		loadWord(A[14],TMP_3+40);
		loadWord(A[15],TMP_3+44);
		loadWord(D[12],TMP_3+48);
		loadWord(D[13],TMP_3+52);
		loadWord(D[14],TMP_3+56);
		loadWord(D[15],TMP_3+60);

		storeWord(FCX,TMP_3);
		FCX<0..19> = PCXI<0..19>;
		PCXI = TMP_1;
		PSW = TMP_2<31..26>::PSW<25..24>::TMP_2<23..7>::CDC<6..0>;
		}
	otawa_sem = "BRANCH(A11); LOAD_CONTEXT;"

op loop_16(b:reg_a, disp:int(4))
   syntax = format("loop %s, %08x", b.syntax, __IADDR + (0b111111111111111111111111111::disp::0b0))
   image = format("%s%4b11111100",b.image,disp)
   action = 	{
   	TMP_1 = 0b111111111111111111111111111::disp::0b0;
		if(b!=0)
		then
			NPC = PC + TMP_1;
		endif;
		b = b-1;
   		}
   	otawa_sem = "SETI(T1, 0); CMP(T1, A(0), T1); IF(NE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1); SETI(T1, 0); SUB(D(0), D(0), T1); "

op jnz_16 = jnz_16_1|jnz_16_2|jnz_a_16|jnz_t_16

op jnz_16_1(disp:int(8))
   syntax = format ("jnz d15, %08x", __IADDR+coerce(int(32),disp)*2)
   image = format("%8b11101110",disp)
   action = 	{
	  	if (D[15]!=0) 
		then
	     		NPC = PC + coerce(int(32),disp)*2;
	  	endif;
   }
   otawa_sem = "SETI(T1, 0), CMP(T1, D15, T1); IF(NE, T1, 2); SETI(T1, IADDR + 2 * S8(0)); BRANCH(T1);"

op jnz_16_2 (b:reg_d, disp:card(4))
   syntax = format ("jnz %s, %08x", b.syntax, __IADDR+coerce(card(32),disp)*2)
   image = format("%s%4b11110110",b.image,disp)
   action = 	{
	  	if (b!=0) 
		then
	     		NPC = PC + coerce(card(32),disp)*2;
	  	endif;
   		}
   	otawa_sem = "SETI(T1, 0); CMP(T1, D(0), T1); IF(NE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op ji_16 (a:reg_a)
   syntax = format("ji %s", a.syntax)
   image = format("0000%s11011100",a.image)
   action = 	{
	  	NPC = a<31..1>::0b0;
   		}
   	otawa_sem = "BRANCH(A(0));"
   	

op jz_a_16 (b:reg_d, disp:card(4))
   syntax = format ("jz.a %s, %08x", b.syntax, __IADDR+coerce(card(32),disp)*2)
   image = format("%s%4b10111100",b.image,disp)
   action = 	{
	  	if (b==0) 
		then
	     		NPC = PC + coerce(card(32),disp)*2;
	  	endif;
   		}
   	otawa_sem = "SETI(T1, 0); CMP(T1, D(0), T1); IF(EQ, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);" 

op jnz_a_16 (b:reg_a, disp:card(4))
   syntax = format ("jnz.a %s, %08x", b.syntax, __IADDR+coerce(card(32),disp)*2)
   image = format("%s%4b01111100",b.image,disp)
   action = 	{
	  	if (b!=0) 
		then
	     		NPC = PC + coerce(card(32),disp)*2;
	  	endif;
   	}
   	otawa_sem = "SETI(T1, 0); CMP(T1, A(0), T1); IF(EQ, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);" 


// jnz.t instruction
op jnz_t_16 (n:card(4),disp:card(4))
   syntax = format ("jnz.t d15, %d, %08x", n, __IADDR+coerce(card(32),disp)*2)
   image = format("%4b%4b10101110",n,disp)
   action = 	{
	  	if (D[15]<n..n>) 
		then
	     		NPC = PC + coerce(card(32),disp)*2;
	  	endif;
   		}
   	otawa_sem = "SCRATCH(T1); IF(EQ, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

/*op lt16 = lt16_reg | lt16_const

op lt16_reg(a:reg_d, b:reg_d)
   syntax = format ("lt d15,%s,%s",a.syntax,b.syntax)
   image = format("%s%s01111010",a.image,b.image)
   action = 	{
	  	D[15] = (a<b);
   		}
   	otawa_sem = "SCRATCH(D15);" 

op lt16_const (a:reg_d, const:const4)
   syntax = format ("lt d15,%s,%s",a.syntax,const.syntax)
   image = format("%s%s11111010",const.image,a.image)
   action = {
		zero_ext32(const, TMP_1, 4);
	  	D[15] = (a<TMP_1);
   	}
   	otawa_sem = "SCRATCH(D15);" */
   
op fret16()
	syntax = "fret"
	image = "0111 XXXX 00000000"
	action = {
		NPC<31..1> = A[11]<31..1>;
		NPC<0..0> = 0;
		TMP_1 = A[10];
		loadWord(A[11],TMP_1);
		A[10] = A[10]+4;
	}
